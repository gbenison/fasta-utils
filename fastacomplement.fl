/*
 * GCB 3mar11
 *
 * Replace each sequence in a FASTA file with its reverse complement.
 */

%x COMMENT
%x SEQUENCE

%{
  #include <glib.h>

  GList *buffer = NULL;
  #define PUSH {buffer = g_list_prepend(buffer, (gpointer)(int)(*yytext));}

  static const int column_width = 40;

  static char complement(char x)
  {
    switch (x) {
      case 'G' : return 'C';
      case 'C' : return 'G';
      case 'A' : return 'T';
      case 'T' : return 'A';
      default  : return '-';
    };
  }

  static void report_sequence()
  {
    int i = 0;
    GList *ptr;
    for (ptr = buffer; ptr != NULL; ptr = ptr->next, i++)
    {
      if ((i > 0) && ((i % column_width) == 0))
        putchar('\n');
      putchar(complement((char)GPOINTER_TO_UINT(ptr->data)));
    }
  }
%}

%%
<INITIAL>[;>]	    {BEGIN(COMMENT); ECHO;}
<COMMENT>\n	    {BEGIN(INITIAL); ECHO;}
<COMMENT>.          {ECHO;}
<INITIAL>[AGTC]|-   {BEGIN(SEQUENCE); g_list_free(buffer); buffer = NULL; PUSH;}
<SEQUENCE>[AGTC]|-  {PUSH;}
<SEQUENCE>[;>]	    {BEGIN(COMMENT); report_sequence();}
<SEQUENCE><<EOF>>   {report_sequence(); yyterminate();}
<*>.|\n 	    {/* default rule - gobble */}
